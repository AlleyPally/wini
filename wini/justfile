# Repository:    https://github.com/casey/just
# Documentation: https://just.systems/man


# Enter the development environment
[group: "wini"]
env:
    nix develop -c "$SHELL"

# Run in dev
[group: "build"]
run:
# IFFEAT nushell
    @nu ./scripts/run.nu
# ENDIF
# IFFEAT posix-sh
    @./scripts/run.sh
# ENDIF

[group: "check"]
check: compile-ts compile-scss
    cargo check

# Create the binary for the production
[group: "build"]
build-prod: compile-ts compile-scss
    ENV_TYPE="PROD" cargo build --release

# Run in production
[group: "build"]
run-prod: js-i js-sync-packages compile-ts compile-scss
    ENV_TYPE="PROD" cargo run --release

# Create the binary for the staging
[group: "build"]
build-staging: compile-ts compile-scss
    ENV_TYPE="STAGING" cargo build --release

# Run in staging
[group: "build"]
run-staging: js-i js-sync-packages compile-ts compile-scss
    ENV_TYPE="STAGING" cargo run --release



# Create a new *something* based on a template
[group: "wini"]
new kind:
# IFFEAT nushell
    @nu ./scripts/run.nu {{kind}}
# ENDIF
# IFFEAT posix-sh
    @./scripts/new.sh {{kind}}
# ENDIF

# Format
[group: "check"]
fmt:
# IFFEAT nushell
    @nu ./scripts/fmt.nu
# ENDIF
# IFFEAT posix-sh
    @./scripts/fmt.sh
# ENDIF

# Lint
[group: "check"]
lint:
# IFFEAT nushell
    @nu ./scripts/lint.nu
# ENDIF
# IFFEAT posix-sh
    @./scripts/lint.sh
# ENDIF

# Synchronises the template that you're using by pulling latest commits
[group: "wini"]
sync-template:
# IFFEAT nushell
    @nu ./scripts/sync-template.nu
# ENDIF
# IFFEAT posix-sh
    @./scripts/sync-template.sh
# ENDIF

# Synchronises the `last_commit_hash` from your wini.toml
[group: "wini"]
sync-commit-hash:
# IFFEAT nushell
    @nu ./scripts/sync-commit-hash.nu
# ENDIF
# IFFEAT posix-sh
    @./scripts/sync-commit-hash.sh
# ENDIF

# Adds a javascript package
[group: "javascript"]
js-add pkg: && js-sync-packages
    bun a {{pkg}}
    @echo -e "\e[1m=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-="
    bun i
    @echo -e "\e[1m=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-="
    @echo -e "Adding {{pkg}} to ./packages-files.toml\e[0m"
# IFFEAT posix-sh
    @./scripts/ask-path-packages.sh {{pkg}}
# ENDIF
# IFFEAT nushell
    @nu ./scripts/ask-path-packages.nu {{pkg}}
# ENDIF

# Removes a javascript package
[group: "javascript"]
js-rm pkg: && js-sync-packages
    bun rm {{pkg}}
    @sed -i '/^{{pkg}}\s*=/d' ./packages-files.toml

# Update javascript packages to their new version
[group: "javascript"]
js-update: && js-sync-packages
    bun update

[group: "javascript"]
js-i:
    bun i

# Sync all the javascript packages in ./public/modules/
[group: "javascript"]
js-sync-packages:
# IFFEAT posix-sh
    @./scripts/sync-packages.sh
# ENDIF
# IFFEAT nushell
    @nu ./scripts/sync-packages.nu
# ENDIF


# Compile and watch for SCSS files
[group: "build"]
compile-scss:
    @echo -e '\e[34m[\e[32m+\e[34m]\e[0m Reloading SCSS...'
# IFFEAT posix-sh
    @./scripts/scss.sh
# ENDIF
# IFFEAT nushell
    @nu ./scripts/scss.nu
# ENDIF

# Compile and watch for Typescript files
[group: "build"]
compile-ts:
    @echo -e '\e[34m[\e[32m+\e[34m]\e[0m Reloading TypeScript...'
# IFFEAT posix-sh
    @./scripts/typescript.sh
# ENDIF
# IFFEAT nushell
    @nu ./scripts/typescript.nu
# ENDIF

# Terminate process running on port `PORT` and start a new server
[group: "build"]
clean-launch:
# IFFEAT posix-sh
    @./scripts/launch.sh
# ENDIF
# IFFEAT nushell
    @nu ./scripts/launch.nu
# ENDIF

on-install:
# IFFEAT posix-sh
    @./scripts/on-install.sh
# ENDIF
# IFFEAT nushell
    @nu ./scripts/on-install.nu
# ENDIF
